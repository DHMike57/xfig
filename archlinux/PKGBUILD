# Maintainer: ml
# Original Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contirbutor: Eric BÃ©langer <eric@archlinux.org>

pkgname=xfig-git
_pkgname=xfig
pkgver=3.2.9.r48.gbc0b82d
pkgrel=1
pkgdesc="An interactive drawing tool"
arch=('x86_64' 'i686')
url="http://mcj.sourceforge.net/"
license=('custom')
depends=('libjpeg' 'xaw3d' 'xorg-fonts-75dpi' 'xorg-fonts-100dpi' 'libpng' 'libxpm' 'ghostscript' 'libxft')
optdepends=('fig2dev: to use the frontend to convert fig files')
makedepends=('fig2dev' 'libxaw')
conflicts=('xfig')
provides=('xfig')
options=('!makeflags' '!emptydirs')
source=("git+https://github.com/DHMike57/xfig.git#branch=pstk"
	xfig.patch LICENSE)
sha1sums=('SKIP'
          '20fbdeb74f5405f729033e74a368ce5b88ba902f'
          '31edf4cfab708820ea3f114d095dfef5aa88e5aa')

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}
prepare() {
  cd ${_pkgname}
  patch -Np2 -b -z .orig -i ../xfig.patch
}

build() {
  cd ${_pkgname}
  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd ${_pkgname}

  make DESTDIR="${pkgdir}" install
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
